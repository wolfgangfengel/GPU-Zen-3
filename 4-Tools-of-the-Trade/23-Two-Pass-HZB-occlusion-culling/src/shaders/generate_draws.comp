#version 460

#extension GL_EXT_control_flow_attributes: require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_EXT_shader_16bit_storage: require
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : require
#extension GL_KHR_shader_subgroup_ballot: require
#extension GL_KHR_shader_subgroup_vote: require

#include "shader_common.h"

layout(local_size_x = kShaderGroupSize) in;
layout(local_size_y = 1) in;
layout(local_size_z = 1) in;

layout(binding = 0) uniform UniformBufferObject { PerFrameData perFrameData; };
layout(binding = 1) readonly buffer Meshes { Mesh meshes[]; };
layout(binding = 2) readonly buffer PerDrawDataVector { PerDrawData perDrawDataVector[]; };
layout(binding = 3) writeonly buffer DrawCommands { DrawCommand drawCommands[]; };
layout(binding = 4) buffer DrawCount { uint drawCount; };
layout(binding = 5) buffer MeshVisibility { uint meshVisibility[]; };
layout(binding = 6) uniform sampler2D hzb;

layout (push_constant) uniform block
{
    PerPassData perPassData;
};

shared uint drawOffset;

void main()
{
	uint groupThreadIndex = gl_LocalInvocationID.x;
	uint drawIndex = gl_GlobalInvocationID.x;

	if (drawIndex >= perFrameData.maxDrawCount)
	{
		return;
	}
	
	bool bPrepass = perPassData.bPrepass == 1;

	PerDrawData perDrawData = perDrawDataVector[drawIndex];
	Mesh mesh = meshes[perDrawData.meshIndex];
	
	vec3 center = (perDrawData.model * vec4(
		mesh.center[0],
		mesh.center[1],
		mesh.center[2], 1.0)).xyz;
	
	// TODO-MILKRU: Multiply mesh bounding spheres by scale.
	// float scaledRadius = scale mesh.radius
	
	bool bVisible = bPrepass ? meshVisibility[drawIndex] == 1 : true;
	
	bool bFrustumCullingEnabled = perFrameData.bMeshFrustumCullingEnabled == 1;
	if (bVisible && bFrustumCullingEnabled)
	{
		bool bFrustumCulled = false;
		
		[[unroll]]
		for(int i = 0; i < kFrustumPlaneCount; ++i)
		{
			bFrustumCulled = bFrustumCulled ||
				dot(vec4(center, 1.0), perFrameData.freezeFrustumPlanes[i]) + mesh.radius < 0.0;
		}
		
		bVisible = !bFrustumCulled;
	}
	
	if (!bPrepass)
	{
		bool bOcclusionCullingEnabled = perFrameData.bMeshOcclusionCullingEnabled == 1;
		if (bVisible && bOcclusionCullingEnabled)
		{
			vec3 centerViewSpace = (perFrameData.freezeView * vec4(center, 1.0)).xyz;
			float P00 = perFrameData.projection[0][0];
			float P11 = perFrameData.projection[1][1];
			float zNear = perFrameData.projection[3][2];
			vec4 AABB;

			if (tryCalculateSphereBounds(centerViewSpace, mesh.radius, zNear, P00, P11, AABB))
			{
				float boundsWidth = (AABB.z - AABB.x) * float(perFrameData.hzbSize);
				float boundsHeight = (AABB.w - AABB.y) * float(perFrameData.hzbSize);
				float mipIndex = floor(log2(max(boundsWidth, boundsHeight)));

				float occluderDepth = textureLod(hzb, 0.5 * (AABB.xy + AABB.zw), mipIndex).x;
				float nearestBoundsDepth = zNear / (-centerViewSpace.z - mesh.radius);

				bool bOcclusionCulled = occluderDepth >= nearestBoundsDepth;
				bVisible = !bOcclusionCulled;
			}
		}
	}
	
	bool bMeshletOcclusionCullingEnabled = perFrameData.bMeshletOcclusionCullingEnabled == 1 && perFrameData.bMeshShadingPipelineEnabled == 1;
	bool bDrawMesh = bPrepass || bMeshletOcclusionCullingEnabled ? bVisible : bVisible && meshVisibility[drawIndex] == 0;
	uvec4 drawMeshBallot = subgroupBallot(bDrawMesh);

	uint drawOffset;
	if (subgroupElect())
	{
		uint drawMeshCount = subgroupBallotBitCount(drawMeshBallot);
		drawOffset = atomicAdd(drawCount, drawMeshCount);
	}
	drawOffset = subgroupBroadcastFirst(drawOffset);
	
	float meshToCameraDistance = distance(center, perFrameData.freezeCameraPosition.xyz);
	uint lodIndex = uint(max(1.0 +
		log(meshToCameraDistance / perFrameData.lodTransitionBase) /
		log(perFrameData.lodTransitionStep), 0.0));

	lodIndex = perFrameData.forcedLod < 0 ?
		min(lodIndex, mesh.lodCount - 1) :
		min(perFrameData.forcedLod, mesh.lodCount - 1);

	MeshLod meshLod = mesh.lods[lodIndex];
	
	if (bDrawMesh)
	{
		DrawCommand drawCommand;
		drawCommand.indexCount = meshLod.indexCount;
		drawCommand.instanceCount = 1;
		drawCommand.firstIndex = meshLod.firstIndex;
		drawCommand.vertexOffset = mesh.vertexOffset;
		drawCommand.firstInstance = 0;

		drawCommand.taskX = (meshLod.meshletCount + kShaderGroupSize - 1) / kShaderGroupSize;
		drawCommand.taskY = 1;
		drawCommand.taskZ = 1;

		drawCommand.drawIndex = drawIndex;
		drawCommand.lodIndex = lodIndex;
		drawCommand.meshVisibility = meshVisibility[drawIndex];
		
		uint drawMeshIndex = subgroupBallotExclusiveBitCount(drawMeshBallot);

		uint drawCommandIndex = drawOffset + drawMeshIndex;
		drawCommands[drawCommandIndex] = drawCommand;
	}
	
	if (!bPrepass)
	{
		meshVisibility[drawIndex] = bVisible ? 1 : 0;
	}
}
